-- programs ------------------------------------------------

entrypoints Program ;

Program.     Program ::= [Declaration] [Rule];

-- declarations --------------------------------------------

DeclarationTable. Declaration ::= "declare" UIdent "[" TypeName Variable ":" Range "]" MoreDimensionsDeclaration ".";
DeclarationConst. Declaration ::= "const" TypeName UIdent "=" Exp ".";

separator Declaration "";

NoMoreDim.   MoreDimensionsDeclaration ::= ;
MoreDim.     MoreDimensionsDeclaration ::= "(" [ColumnDeclaration] ")";

ColumnDeclaration. ColumnDeclaration ::= TypeName Variable;
separator ColumnDeclaration ",";

TypeInt.     TypeName ::= "int";
TypeDouble.  TypeName ::= "double";

Range.       Range ::= Exp ".." Exp;

-- rules ---------------------------------------------------

Rule.     Rule ::= Predicate [RuleGoal] ".";

RuleGoal. RuleGoal ::= ":-" [OneGoal];
separator nonempty RuleGoal ";";

separator Rule "" ;

PredicateAtom.   Predicate ::= Atom;
PredicateStruct. Predicate ::= Structure;

GoalPredicate.  OneGoal ::= Predicate;
GoalEquation.   OneGoal ::= Equation;

separator nonempty OneGoal "," ;

TermAtom.        Term ::= Atom;
TermValue.       Term ::= Value;
TermCall.	     Term ::= DIdent "(" [Term] ")";

ValueInt.        Value ::= Integer;
ValueVar.        Value ::= Variable;
ValueDouble.	 Value ::= Double;
ValueConst.      Value ::= Constant;

separator nonempty Term "," ;

Variable.        Variable ::= LIdent;

Constant.        Constant ::= UIdent;

AtomSharded.     Atom ::= UIdent "[" Value "]";
AtomSingle.      Atom ::= UIdent;

Structure.       Structure ::= Atom "(" [Term] ")" ;

Comparison.     Equation ::= Exp CompOp Exp;
CompOpEq.       CompOp ::= "=";
CompOpGt.       CompOp ::= ">";
CompOpLt.       CompOp ::= "<";

EAdd.            Exp ::= Exp "+" Exp1;
ESub.            Exp ::= Exp "-" Exp1;
EMul.            Exp1 ::= Exp1 "*" Exp2;
EDiv.            Exp1 ::= Exp1 "/" Exp2;
EInt.            Exp2 ::= Value;
coercions Exp 2;              

token UIdent (upper (letter | digit | '_')*) ;
token LIdent (lower (letter | digit | '_')*) ;
token DIdent '$' (upper (letter | digit | '_')*) ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

